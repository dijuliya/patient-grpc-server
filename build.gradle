buildscript {
    ext {
        springBootVersion = '2.3.0.RELEASE'
        grpcVersion = '1.29.0'
        protobufVersion = '3.11.0'
        lombokVersion = '1.18.6'
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id 'java'
    id 'com.google.protobuf' version '0.8.12'
}

group 'ru.rtlabs'
version '1.0-SNAPSHOT'

sourceCompatibility = 11
compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

dependencies {
    compile "com.google.protobuf:protobuf-java:${protobufVersion}",
            "javax.annotation:javax.annotation-api:1.3.2",
            "io.grpc:grpc-all:${grpcVersion}",
            "org.springframework.boot:spring-boot-starter:${springBootVersion}",
            "net.devh:grpc-server-spring-boot-starter:2.7.0.RELEASE"

    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}